group 'com.rnaufal'
version '1.0.0-SNAPSHOT'

buildscript {
    repositories {
        jcenter()
        maven {
            url "http://repo.spring.io/snapshot"
        }
        maven {
            url "http://repo.spring.io/milestone"
        }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.4.2.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'idea'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        java.srcDir file('src/integrationTest/java')
        resources.srcDir file('src/integrationTest/resources')
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

idea {
    module {
        testSourceDirs += file('src/integrationTest/java')
        scopes.TEST.plus += [configurations.integrationTestCompile,
                             configurations.integrationTestRuntime]
    }
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
}

check.dependsOn integrationTest
bootRun.dependsOn check

dependencies {
    compile("org.springframework.boot:spring-boot-starter-web") {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-tomcat'
    }
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.apache.commons:commons-lang3:3.4")
    compile("com.google.guava:guava:18.0")
    runtime("org.hsqldb:hsqldb")
    testCompile("junit:junit:4.12")
    testCompile("org.mockito:mockito-core:1.10.19")
    testCompile("org.hamcrest:hamcrest-library:1.3")
    testCompile("org.hamcrest:hamcrest-core:1.3")
    testCompile("com.jayway.jsonpath:json-path")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.springframework:spring-test:4.3.4.RELEASE")
}
